buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'xmlunit', name: 'xmlunit', version: '1.6'
    }
}

plugins {
    id 'de.dfki.mary.voicebuilding-base'
}

voice {
    name = voiceName
    license {
        name = voiceLicenseName
        url = voiceLicenseUrl
    }
}
def voiceLocale = new Locale.Builder()
        .setLanguage(voiceLocaleLanguage)
        .setRegion(voiceLocaleRegion)
        .build()

task testPlugins(group: 'Verification') << {
    assert plugins.findPlugin('java')
    assert plugins.findPlugin('de.dfki.mary.voicebuilding-base')
}

task testVoiceProps(group: 'Verification') << {
    assert voice.name == voiceName
    assert voice.language == voiceLocale.language
    assert voice.region == voiceLocale.country
    assert voice.nameCamelCase == voiceNameCamelCase
    assert voice.locale == voiceLocale
    assert voice.localeXml == voiceLocale.toLanguageTag()
    assert voice.description == voiceDescription
    assert voice.license?.name == voiceLicenseName
}

task testJavaCompatibility(group: 'Verification') << {
    assert "$sourceCompatibility" == '1.7'
    assert "$targetCompatibility" == '1.7'
}

task testGenerateSource(group: 'Verification') {
    dependsOn generateSource
    doLast {
        assert file("$buildDir/generatedSrc/main/java/marytts/voice/$voice.nameCamelCase/Config.java").exists()
    }
}

task testCompileJava(group: 'Verification') {
    dependsOn compileJava
    doLast {
        assert file("$buildDir/classes/main/marytts/voice/$voice.nameCamelCase/Config.class").exists()
    }
}

task testCompileTestJava(group: 'Verification') {
    dependsOn compileTestJava
    doLast {
        assert file("$buildDir/classes/test/marytts/voice/$voice.nameCamelCase/ConfigTest.class").exists()
    }
}

task testCompileIntegrationTestGroovy(group: 'Verification') {
    dependsOn compileIntegrationTestGroovy
    doLast {
        assert file("$buildDir/classes/integrationTest/marytts/voice/$voice.nameCamelCase/LoadVoiceIT.class").exists()
    }
}

task testGenerateVoiceConfig(group: 'Verification') {
    dependsOn generateVoiceConfig
    doLast {
        def configFile = file("$buildDir/resources/main/marytts/voice/$voice.nameCamelCase/voice.config")
        assert configFile.exists()
        def actual = new Properties()
        configFile.withInputStream {
            actual.load it
        }
        def expected = [
                name                             : voiceName,
                locale                           : "$voiceLocale",
                'unitselection.voices.list'      : voiceName,
                "voice.${voiceName}.domain"      : 'general',
                "voice.${voiceName}.gender"      : voiceGender,
                "voice.${voiceName}.locale"      : "$voiceLocale",
                "voice.${voiceName}.samplingRate": '16000'
        ] as Properties
        assert actual == expected
    }
}

task testGenerateServiceLoader(group: 'Verification') {
    dependsOn generateServiceLoader
    doLast {
        def serviceLoaderFile = file("$buildDir/resources/main/META-INF/services/marytts.config.MaryConfig")
        assert serviceLoaderFile.exists()
        assert serviceLoaderFile.text == "marytts.voice.${voice.nameCamelCase}.Config"
    }
}

import org.custommonkey.xmlunit.XMLUnit
import org.custommonkey.xmlunit.examples.RecursiveElementNameAndTextQualifier

task testGeneratePom(group: 'Verification') {
    dependsOn generatePom
    doLast {
        def pomFile = file("$buildDir/resources/main/META-INF/maven/$project.group/voice-$voiceName/pom.xml")
        assert pomFile.exists()
        def pomXml = new groovy.xml.StreamingMarkupBuilder().bind {
            project(xmlns: "http://maven.apache.org/POM/4.0.0",
                    'xmlns:xsi': "http://www.w3.org/2001/XMLSchema-instance",
                    'xsi:schemaLocation': "http://maven.apache.org/POM/4.0.0 http://maven.apache.org/xsd/maven-4.0.0.xsd") {
                modelVersion '4.0.0'
                groupId project.group
                artifactId projectDir.name
                delegate.version project.version
                delegate.description voiceDescription
                licenses {
                    license {
                        delegate.name voiceLicenseName
                        url voiceLicenseUrl
                    }
                }
                delegate.dependencies {
                    dependency {
                        groupId 'junit'
                        artifactId 'junit'
                        delegate.version '4.12'
                        scope 'test'
                    }
                    dependency {
                        groupId 'de.dfki.mary'
                        artifactId 'marytts-runtime'
                        delegate.version maryVersion
                        scope 'compile'
                    }
                    dependency {
                        groupId 'de.dfki.mary'
                        artifactId "marytts-lang-$voiceLocale.language"
                        delegate.version maryVersion
                        scope 'runtime'
                    }
                }
            }
        } as String
        XMLUnit.ignoreWhitespace = true
        def diff = XMLUnit.compareXML(pomFile.text, pomXml)
        diff.overrideElementQualifier(new RecursiveElementNameAndTextQualifier())
        assert diff.similar()
    }
}

task testGeneratePomProperties(group: 'Verification') {
    dependsOn generatePomProperties
    doLast {
        def pomPropertiesFile = file("$buildDir/resources/main/META-INF/maven/$project.group/voice-$voiceName/pom.properties")
        assert pomPropertiesFile.exists()
        assert pomPropertiesFile.readLines() == ["version=$version", "groupId=$project.group", "artifactId=$projectDir.name"]
    }
}

import java.util.zip.ZipFile

task testJar(group: 'Verification') {
    dependsOn jar
    doLast {
        def actual = new ZipFile(jar.archivePath).entries().findAll { !it.isDirectory() }.collect { it.name } as Set
        def expected = [
                'META-INF/MANIFEST.MF',
                'META-INF/services/marytts.config.MaryConfig',
                "META-INF/maven/$project.group/voice-$voiceName/pom.xml",
                "META-INF/maven/$project.group/voice-$voiceName/pom.properties",
                "marytts/voice/$voice.nameCamelCase/Config.class",
                "marytts/voice/$voice.nameCamelCase/voice.config"
        ] as Set
        assert actual == expected
    }
}
