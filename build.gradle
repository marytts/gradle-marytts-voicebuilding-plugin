plugins {
    id 'groovy'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.5.3-SNAPSHOT'

repositories {
    jcenter()
    maven {
        url 'https://oss.jfrog.org/artifactory/repo'
    }
}

def maryttsVersion = '5.1.2'

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion
    compile 'commons-codec:commons-codec:1.9'
    ['de', 'en', 'fr', 'it', 'ru', 'sv', 'te', 'tr'].each { locale ->
        runtime group: 'de.dfki.mary', name: "marytts-lang-$locale", version: maryttsVersion
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url 'https://api.bintray.com/maven/marytts/marytts/gradle-marytts-voicebuilding-plugin'
            if (hasProperty('bintrayUser') && hasProperty('bintrayKey')) {
                credentials {
                    username bintrayUser
                    password bintrayKey
                }
            } else {
                logger.warn 'Properties bintrayUser and bintrayKey not set; publishing will fail'
            }
        }
    }
}

wrapper.gradleVersion '2.3'
