plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group 'de.dfki.mary'
version '5.3.0-SNAPSHOT'
description 'A replacement (incubating) for the legacy VoiceImportTools in MaryTTS'

gradlePlugin {
    plugins {
        base {
            id = 'de.dfki.mary.voicebuilding-base'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingBasePlugin'
        }
        data {
            id = 'de.dfki.mary.voicebuilding-data'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingDataPlugin'
        }
        festvox {
            id = 'de.dfki.mary.voicebuilding-festvox'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingFestvoxPlugin'
        }
        legacy {
            id = 'de.dfki.mary.voicebuilding-legacy'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingLegacyPlugin'
        }
    }
}

pluginBundle {
    website = 'http://mary.dfki.de'
    vcsUrl = 'https://github.com/marytts/gradle-marytts-voicebuilding-plugin'
    description = 'A replacement for the legacy VoiceImportTools in MaryTTS'
    tags = ['TTS', 'speech synthesis', 'voicebuilding']
    plugins {
        base {
            id = 'de.dfki.mary.voicebuilding-base'
            displayName = 'Voicebuilding Base Plugin'
        }
        data {
            id = 'de.dfki.mary.voicebuilding-data'
            displayName = 'Voicebuilding Data Plugin'
        }
        festvox {
            id = 'de.dfki.mary.voicebuilding-festvox'
            displayName = 'Voicebuilding FestVox Plugin'
        }
        legacy {
            id = 'de.dfki.mary.voicebuilding-legacy'
            displayName = 'Voicebuilding Legacy Plugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion, {
        exclude module: 'groovy-all'
    }
    testCompile group: 'org.testng', name: 'testng', version: '6.9.4'
    testCompile group: 'xmlunit', name: 'xmlunit', version: '1.6'
}

processResources << {
    file("$destinationDir/maryttsVersion.txt").text = maryttsVersion
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
    doFirst {
        // path to shared testkit gradle home
        systemProperty 'testProjectDir', temporaryDir
        // dynamic marytts version
        systemProperty 'maryVersion', maryttsVersion
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "https://api.bintray.com/maven/marytts/marytts/$project.name"
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
