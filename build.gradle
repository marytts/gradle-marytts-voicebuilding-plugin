plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

group 'de.dfki.mary'
version '5.5-SNAPSHOT'
description 'A replacement (incubating) for the legacy VoiceImportTools in MaryTTS'

sourceCompatibility = '1.8'

gradlePlugin {
    plugins {
        base {
            id = 'de.dfki.mary.voicebuilding-base'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingBasePlugin'
        }
        data {
            id = 'de.dfki.mary.voicebuilding-data'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingDataPlugin'
        }
        festvox {
            id = 'de.dfki.mary.voicebuilding-festvox'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingFestvoxPlugin'
        }
        legacy {
            id = 'de.dfki.mary.voicebuilding-legacy'
            implementationClass = 'de.dfki.mary.voicebuilding.VoicebuildingLegacyPlugin'
        }
    }
}

pluginBundle {
    website = 'http://mary.dfki.de'
    vcsUrl = 'https://github.com/marytts/gradle-marytts-voicebuilding-plugin'
    description = 'A replacement for the legacy VoiceImportTools in MaryTTS'
    tags = ['TTS', 'speech synthesis', 'voicebuilding']
    plugins {
        base {
            id = 'de.dfki.mary.voicebuilding-base'
            displayName = 'Voicebuilding Base Plugin'
        }
        data {
            id = 'de.dfki.mary.voicebuilding-data'
            displayName = 'Voicebuilding Data Plugin'
        }
        festvox {
            id = 'de.dfki.mary.voicebuilding-festvox'
            displayName = 'Voicebuilding FestVox Plugin'
        }
        legacy {
            id = 'de.dfki.mary.voicebuilding-legacy'
            displayName = 'Voicebuilding Legacy Plugin'
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    implementation localGroovy()
    implementation group: 'de.dfki.mary', name: 'marytts-voicebuilding', version: maryttsVBVersion, {
        exclude group: '*', module: 'groovy-all'
        exclude group: '*', module: 'mwdumper'
        exclude group: '*', module: 'sgt'
    }
    implementation group: 'org.m2ci.msp', name: 'gradle-praat-wrapper-plugin', version: '0.6'
    implementation group: 'org.m2ci.msp', name: 'jtgt', version: '0.6.1'
    testImplementation group: 'org.testng', name: 'testng', version: '7.0.0'
    testImplementation group: 'xmlunit', name: 'xmlunit', version: '1.6'
}

processResources {
    doLast {
        file("$destinationDir/maryttsVersion.txt").text = maryttsVersion
    }
}

test {
    useTestNG {
        parallel = 'classes'
    }
    testLogging {
        events 'started', 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
    failFast = true
    doFirst {
        // path to shared testkit gradle home
        systemProperty 'testProjectDir', temporaryDir
        // dynamic marytts version
        systemProperty 'maryVersion', maryttsVersion
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/gradle-marytts-voicebuilding-plugin'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
